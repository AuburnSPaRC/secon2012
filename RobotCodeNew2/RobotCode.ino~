/**  Main code for operation of SECON robot.
* 
* Includes navigation system and line-following system, as well as calibration
* and setup routines. For reference, high values from line sensor are black, 
* low are white.
*
* Depends: MotionFunctions.ino
*          MotorFunctions.ino
*          TaskSensorFunctions.ino
*          PID_v1.h
*          PololuQTRSensors.h
*
* Credits: Tyler Crumpton
*
*/
#include <PID_v1.h>
#include <PololuQTRSensors.h>

//#define CALIBRATE_MOTORS   // Run in motor calibration mode.
//#define CALIBRATE_ENCODERS  // Run in encoder calibration mode.
//#define DEBUG_MOTOR_SPEED  // Display motor speeds in Serial window.
//#define DEBUG_LINE         // Display line sensor data
//#define DEBUG_COURSE       // Display info on robot's course location


#define NUM_SENSORS   8     // number of sensors used on each array
#define TIMEOUT       2500  // waits for 2500 us for sensor outputs to go low
#define MID_LINE      3500  // value of sensor when line is centered (0-7000)
#define WHITE_LINE    1     // '1' = white line, '0' = black line

#define REFLECT_THRESHOLD 750  // part of 1000 at which line is not found

// Direction Definitions
#define LEFT    true    // Left direction 
#define RIGHT   false   // Right direction

// Turn Conditions
#define ON_LINE       0 // Bot is on the line
#define AT_T          1 // Bot is at a T-intersection
#define AT_LEFT       2 // Bot is at left turn
#define AT_RIGHT      3 // Bot is at right turn
#define OFF_LINE      4 // Bot is off the line

// Movement Actions
#define STOP          0 // Bot completely stops
#define TURN_LEFT     1 // Bot turns ~90 degrees left
#define TURN_RIGHT    2 // Bot turns ~90 degrees right
#define MOVE_FORWARD  3 // Bot moves forward at FULL_SPEED

#define MAX_VELOCITY  255  // Maximum motor velocity

#define FULL_SPEED    0.40 // Fraction of MAX_VELOCITY that is 'full' speed 
#define TURN_SPEED    0.25 // Fraction of MAX_VELOCITY that is 'turning' speed 
//#define TURN_TIME     570  // Number of mSeconds to turn robot 90 degrees
#define TURN_STOPS    10   // Number of encoder stops for a bot 90 degree turn

#define TL_NEGONE_COUNT 200
#define TL_ZERO_COUNT   200
#define TL_ONE_COUNT    200
#define TL_TWO_COUNT    200

#define ML_ZERO_COUNT   200
#define ML_THREE_COUNT  400
#define ML_FOUR_COUNT   400
#define ML_SEVEN_COUNT  400

// Maximum difference in wheel speeds when line-following
#define MAX_PID_DELTA (1-FULL_SPEED)*MAX_VELOCITY  
// PWM offset for motor speeds to be equal (Left motor is faster = +)
#define MOTOR_OFFSET  0

union u_double
{
  byte b[4];
  float dval;
};  //A structure to read in floats from the serial ports
  




// Course Locations
boolean leftRightLoc = RIGHT;  // (RIGHT or LEFT)
short   taskLoc      = -1;     // (-1 to 2)
short   mainLoc      = 0;      // (0 to 7)

// PID Coeffs
double KP = .015; //.015
double KI = .0001; //.0001
double KD = .001; //.001

// Movement speeds
double leftDelta = 0;
double rightDelta = 0;
double forwardSpeed = 0;

int delayCounter = 0;

// Change these pins when you need to
unsigned char fSensorPins[] = {33,35,37,39,41,43,45,47};
unsigned char lEncoderPins[] = {46};
unsigned char rEncoderPins[] = {48};

//unsigned char rSensorPins[] = {2,3,4,5,6,7,8,9};
#define LEFT_PWM_PIN   10
#define LEFT_DIR_PIN   11
#define RIGHT_PWM_PIN  12
#define RIGHT_DIR_PIN  13
//#define CAL_LED_PIN    5
#define RELAY_K1_PIN   52
#define RELAY_K2_PIN   53
#define TEST_LED_PIN   51
#define LEFT_ENC_PIN   36
#define RIGHT_ENC_PIN  50


// Sensors (f)(c)0 through (f)(c)7 are connected to (f)(c)SensorPins[]
PololuQTRSensorsRC fSensor(fSensorPins, NUM_SENSORS, TIMEOUT); 
//PololuQTRSensorsRC rSensor(rSensorPins, NUM_SENSORS, TIMEOUT); 
PololuQTRSensorsRC lEncoder(lEncoderPins, 1, TIMEOUT);//, LEFT_ENC_PIN);
PololuQTRSensorsRC rEncoder(rEncoderPins, 1, TIMEOUT);//, RIGHT_ENC_PIN); 
unsigned int fSensorValues[NUM_SENSORS];
//unsigned int rSensorValues[NUM_SENSORS];
unsigned int lEncoderValues[1];
unsigned int rEncoderValues[1];


// Setup PID computation
double setpointPID, inputPID, outputPID;
PID lfPID(&inputPID, &outputPID, &setpointPID, KP, KI, KD, DIRECT);

void setup()
{
  Serial.begin(9600);        // Begin serial comm for debugging  
  delay(500);                // Wait for serial comm to come online
  
  // Setup pin IO:
  pinMode(LEFT_PWM_PIN, OUTPUT);
  pinMode(LEFT_DIR_PIN, OUTPUT);
  pinMode(RIGHT_PWM_PIN, OUTPUT);
  pinMode(RIGHT_DIR_PIN, OUTPUT);
  //pinMode(CAL_LED_PIN, OUTPUT);
  pinMode(RELAY_K1_PIN, OUTPUT);
  pinMode(RELAY_K2_PIN, OUTPUT);
  pinMode(TEST_LED_PIN, OUTPUT);
  
  digitalWrite(TEST_LED_PIN, LOW);

  delay(50);
  
  digitalWrite(RELAY_K1_PIN, 0);
  digitalWrite(RELAY_K2_PIN, 0);
  
  motorCalibrate();          // Does nothing ifdef CALIBRATE_MOTORS
  
  setpointPID = MID_LINE;    // Set the point we want our PID loop to adjust to
    
  lfPID.SetMode(AUTOMATIC);  // turn on the PID
  lfPID.SetOutputLimits(-MAX_PID_DELTA, MAX_PID_DELTA); // force PID to the range of motor speeds. 
  
  calibrateSensors(); // Calibrate line sensors
  
  delay(5000);
  
  // Start movement (currently starting at mainLoc 'M0')
  mainLoc = 0;
  setMove(MOVE_FORWARD);
 
}


void dynamic_PID() // Sets the PID coefficients dynamically via a serial command interface...
{
  char command;
  int i,j;
  u_double dVals[3];
  
   
  
  if(Serial.available())
  {
    delay(100);
      command=Serial.read();
      switch (command)
      {
        case 'p':
          Serial.print("Print!");
          break;
        
        case 'd':
          if(Serial.available()>=12)
          {
            for(i=0;i<3;i++)
            {
              for(j=0;j<4;j++)
              {
                dVals[i].b[j]=Serial.read();
              }
              Serial.print("\n");
              Serial.print(dVals[i].dval);
              Serial.print("\n");Serial.flush();
            }
          }
          break;
       }
  }
}

void loop()
{ 
  delayCounter++;
  //if (delayCounter>200)
    //digitalWrite(TEST_LED_PIN,HIGH);
  int turnType = isTurn();
  dynamic_PID();
  switch (mainLoc)
  {
    case 0: // At mainLoc M0
      followLine(); // Follow the line until turn is detected
      if ((turnType == AT_RIGHT || turnType == OFF_LINE) && delayCounter > ML_ZERO_COUNT) 
      {
        turnRight(); // Should only detect a right turn
        mainLoc = 1; // Bot is now at mainLoc M1
        setMove(MOVE_FORWARD); // Begin moving forward again
        delayCounter = 0;
      }
      break;
    case 1: // At mainLoc M1
      navigateTask();
      break;
    case 2: // At mainLoc M2
      navigateTask();
      break;
    case 3: // At mainLoc M3
      followLine(); // Follow the line unless turn is detected
      if ((turnType == AT_RIGHT || turnType == OFF_LINE) && delayCounter > ML_THREE_COUNT) 
      {
        turnRight(); // Should only detect a right turn
        mainLoc = 4; // Bot is now at mainLoc M4
        setMove(MOVE_FORWARD); // Begin moving forward again
        delayCounter = 0;
      }
      break;
    case 4: // At mainLoc M4
      followLine(); // Follow the line unless turn is detected
      if ((turnType == AT_RIGHT || turnType == OFF_LINE) && delayCounter > ML_FOUR_COUNT) 
      {
        turnRight(); // Should only detect a right turn
        mainLoc = 5; // Bot is now at mainLoc M5
        setMove(MOVE_FORWARD); // Begin moving forward again
        delayCounter = 0;
      }
      break;
    case 5: // At mainLoc M5
      navigateTask();
      break;
    case 6: // At mainLoc M6
      navigateTask();
      break;
    case 7: // At mainLoc M7
      followLine(); // Follow the line unless turn is detected
      if ((turnType == AT_RIGHT || turnType == OFF_LINE) && delayCounter > ML_SEVEN_COUNT) 
      {
        turnRight(); // Should only detect a right turn
        mainLoc = 0; // Bot is now at mainLoc M0
        setMove(MOVE_FORWARD); // Begin moving forward again
        delayCounter = 0;
      }
      break;
  }
}

// Checks to see if the robot is at a turn or a 'T', by checking the outer sensors.
// NOTE: This assumes white line on black surface.
int isTurn()
{
  boolean isLeft  = (fSensorValues[0] < REFLECT_THRESHOLD);
  boolean isRight = (fSensorValues[7] < REFLECT_THRESHOLD);
  boolean isOff = true;
  
  // Checks to see if every sensor is above threshold:
  for (int i = 0; i < NUM_SENSORS; ++i)
  {
    isOff &= (fSensorValues[i] >= REFLECT_THRESHOLD);
  }
  
  if (isLeft && isRight) 
    return AT_T;
  else if (isLeft) 
    return AT_LEFT;
  else if (isRight) 
    return AT_RIGHT;
  else if (isOff)
    return OFF_LINE;
  else 
    return ON_LINE;
}

// Does PID for line folliwing and sets the motor delta speeds.
void followLine()
{
  // Read calibrated front sensor values and obtain a measure of the line position from 0 to 7000
  unsigned int position = fSensor.readLine(fSensorValues, QTR_EMITTERS_ON, WHITE_LINE);                            

  inputPID = position;            // set PID input to position of line
  lfPID.Compute();                // compute correction, store in outputPID
  if (outputPID < 0)
    rightDelta = outputPID;         // sets right wheel's speed variation
  else
    leftDelta  = -outputPID;        // sets left wheel's speed variation
  updateMotors();
  
  #ifdef DEBUG_LINE
    Serial.print("Position: ");
    Serial.print(position);
    Serial.print("S[0]: ");
    Serial.print(fSensorValues[0]);
    Serial.print("\tS[1]: ");
    Serial.print(fSensorValues[1]);
    Serial.print("\tS[2]: ");
    Serial.print(fSensorValues[2]);
    Serial.print("\tS[3]: ");
    Serial.print(fSensorValues[3]);
    Serial.print("\tS[4]: ");
    Serial.print(fSensorValues[4]);
    Serial.print("\tS[5]: ");
    Serial.print(fSensorValues[5]);
    Serial.print("\tS[6]: ");
    Serial.print(fSensorValues[6]);
    Serial.print("\tS[7]: ");
    Serial.println(fSensorValues[7]);    
  #endif  
  #ifdef DEBUG_COURSE
    Serial.print("Counter: ");
    Serial.print(delayCounter);
    Serial.print("\tMainLoc: M");
    Serial.print(mainLoc);
    Serial.print("\tTaskLoc: T");
    Serial.println(taskLoc);
  #endif
}

//Take a reading from the task sensors and makes L/R decision 
void takeReading()
{
  switch (mainLoc)
  {
    case 1: // Voltage Task
      leftRightLoc = readVoltage();
      break;
    case 2: // Capacitance Task 
      leftRightLoc = readCapacitance();
      break;
    case 5: // Temperature Task
      leftRightLoc = readTemperature();
      break;
    case 6: // Waveform Task
      leftRightLoc = readWaveform();
      break;
    default:
      // Should not occur, means that takeReading was called at the wrong place.
      break;
  }
  digitalWrite(RELAY_K1_PIN, 0);
  digitalWrite(RELAY_K2_PIN, 0);
}

// Navigate around the task and take the sensor measurement
void navigateTask()
{
  int turnType = isTurn();
  switch (taskLoc)
  {
    case -1:
      followLine(); // Follow the line until 'T' is detected
      if ((turnType == AT_T || turnType == OFF_LINE) && delayCounter > TL_NEGONE_COUNT)
      {
        moveToSensor();
        takeReading();
        moveFromSensor();
        if (leftRightLoc == LEFT)
        { 
          turnLeftWheel(-6);
          turnRightWheel(32);
        }
        else 
        {
          turnRightWheel(-6);
          turnLeftWheel(32);
        }
        taskLoc = 0; // Bot is now at taskLoc L/R0
        setMove(MOVE_FORWARD);
        delayCounter = 0;
      }
      break;  
    case 0:
      moveToTurn(); // Follow the line until turn is detected
      //if (delayCounter > TL_ZERO_COUNT)
      //{  
        if (leftRightLoc == RIGHT)// && (turnType == AT_LEFT))// || turnType == OFF_LINE)) // If left turn detected
        {
          turnLeft(); 
          taskLoc = 1;  // Bot is now at taskLoc R1
          setMove(MOVE_FORWARD); // Start moving forward
          delayCounter = 0; // Reset the delay counter
        } 
        else if (leftRightLoc == LEFT)// && (turnType == AT_RIGHT))// || turnType == OFF_LINE)) // If left turn detected
        {
          turnRight(); 
          taskLoc = 1;   // Bot is now at taskLoc L1
          setMove(MOVE_FORWARD); // Start moving forward
          delayCounter = 0; // Reset the delay counter
        }
      //}
      break;
    case 1:
      followLine(); // Follow the line until turn is detected
      if (delayCounter > TL_ONE_COUNT)
      {
        if (leftRightLoc == RIGHT && (turnType == AT_LEFT))// || turnType == OFF_LINE)) // If left turn detected
        {
          turnLeft();    
          taskLoc = 2;   // Bot is now at taskLoc R2
          setMove(MOVE_FORWARD); // Start moving forward
          delayCounter = 0; // Reset the delay counter
        } 
        else if (leftRightLoc == LEFT && (turnType == AT_RIGHT))// || turnType == OFF_LINE)) // If left turn detected
        {
          turnRight();  
          taskLoc = 2;   // Bot is now at taskLoc L2
          setMove(MOVE_FORWARD); // Start moving forward
          delayCounter = 0; // Reset the delay counter
        } 
      }
      break;
    case 2:
      followLine(); // Follow the line until turn is detected
      if (delayCounter > TL_TWO_COUNT)
      {
        if (leftRightLoc == RIGHT && (turnType == AT_RIGHT))// || turnType == OFF_LINE)) // If left turn detected
        {
          turnRight();    
          taskLoc = -1;      // Reset taskLoc
          increaseMainLoc(); // Increments mainLoc by 1
          setMove(MOVE_FORWARD); // Start moving forward
          delayCounter = 0; // Reset the delay counter
        } 
        else if (leftRightLoc == LEFT && (turnType == AT_LEFT))// || turnType == OFF_LINE)) // If left turn detected
        {
          turnLeft();       
          taskLoc = -1;      // Reset taskLoc
          increaseMainLoc(); // Increments mainLoc by 1
          setMove(MOVE_FORWARD); // Start moving forward
          delayCounter = 0; // Reset the delay counter
        } 
      }
      break;
  } 
}

void increaseMainLoc()
{
  ++mainLoc;    // Increment mainLoc
  mainLoc %= 8; // Make sure mainLoc is never > 7
}

void calibrateSensors()
{
  boolean toggle = true;
  setMove(TURN_LEFT);
  // Calibrate sensors  (robot must be fully on the line)
  // Note: still needs calibration motor routine
  for (int i = 0; i < 50; i++)  // Make the calibration take about 5 seconds
  {
    // Reads both sensors 10 times at 2500 us per read (i.e. ~25 ms per call)
    fSensor.calibrate();
    lEncoder.calibrate();
    rEncoder.calibrate();
    digitalWrite(RELAY_K1_PIN, toggle); // Make sound!
    toggle = !toggle;
    //rSensor.calibrate();
  }
  
  toggle = true;
  setMove(TURN_RIGHT);
  for (int i = 0; i < 50; i++)  // Make the calibration take about 5 seconds
  {
    // Reads both sensors 10 times at 2500 us per read (i.e. ~25 ms per call)
    fSensor.calibrate();
    lEncoder.calibrate();
    rEncoder.calibrate();
    digitalWrite(RELAY_K1_PIN, toggle); // Make sound!
    toggle = !toggle;
    //rSensor.calibrate();
  }  
  setMove(STOP);
}


